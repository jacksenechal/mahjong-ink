plugins {
    id 'com.android.application'
}

def gitCommitCountProvider = providers.exec {
    commandLine 'git', 'rev-list', '--count', 'HEAD'
}.standardOutput.asText.map { it.trim().isInteger() ? it.trim().toInteger() : 1 }.orElse(1)

def ciBuildNumberProvider = providers.environmentVariable('GITHUB_RUN_NUMBER')
        .map { it.trim().isInteger() ? it.trim().toInteger() : null }
        .orElse(gitCommitCountProvider)

def versionCodeValue = ciBuildNumberProvider.get()
def versionNameValue = "1.0.${versionCodeValue}"

android {
    namespace 'com.mahjongink'
    compileSdk 34

    defaultConfig {
        applicationId "com.mahjongink"
        minSdk 21
        targetSdk 34
        versionCode versionCodeValue
        versionName versionNameValue

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    lint {
        disable 'ObsoleteLintCustomCheck'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.preference:preference:1.2.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.11.1'
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'org.mockito:mockito-core:5.8.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
